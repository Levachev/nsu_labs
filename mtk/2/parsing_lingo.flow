import string;
import ds/array;
import ds/tree;

export {   

    Int : ();
    Bool : ();
    NoType : ();
    AnyType : ();
    Array : (baseType: ExprType);
    VarType ::= Int, Bool, Array;
    ExprType ::= VarType, NoType, AnyType;


    VarDecl : (name: string, type: VarType);
    VarDeclList : (decls: [VarDecl]);
    varDeclList2s(vdl: VarDeclList) -> string;


    Var : (name: string);


    IntConst : (value: int);
    BoolConst : (value: bool);
    ArrayConst : (values: Tree<int, Const>);
    NonBoolConst ::= IntConst, ArrayConst;
    Const ::= NonBoolConst, BoolConst;
    const2s(c: Const) -> string;


    ArithmAddOp : ();
    ArithmSubOp : ();
    ArithmMulOp : ();
    ArithmDivOp : ();
    ArithmBinaryOp ::= ArithmAddOp, ArithmSubOp, ArithmMulOp, ArithmDivOp;


    ArithmNegOp : ();
    ArithmOp ::= ArithmBinaryOp, ArithmNegOp;


    ArithmBinary : (l: Expr, r: Expr, op: ArithmBinaryOp);
    ArithmNeg : (e: Expr);
    ArithmExpr ::= ArithmBinary, ArithmNeg;


    Equal : ();
    Less : ();
    ComparisonType ::= Equal, Less;
    Comparison : (l: Expr, r: Expr, comparisonType: ComparisonType);


    Not : (e: BoolExpr);
    Or : (l: BoolExpr, r: BoolExpr);
    And : (l: BoolExpr, r:BoolExpr);
    BoolExpr ::= Comparison, Not, Or, And, BoolConst;
    

    ArrayUpdateOp : (i: Expr, v: Expr);
    ArrayApplyOp : (i: Expr);
    ArrayConcatOp : (arr2: Expr);
    ArrayLengthOp : ();
    ArrayOpType ::= ArrayUpdateOp, ArrayApplyOp, ArrayConcatOp, ArrayLengthOp;
    ArrayOp : (arr: Expr, opType: ArrayOpType);


    Expr ::= Var, Const, ArithmExpr, ArrayOp, BoolExpr;
    expr2s(e: Expr) -> string;


    Assignment : (varName: string, expr: Expr);
    BodiesSeq : (bodies: [Body]);
    Test : (b: BoolExpr);
    NonDeterChoice : (bodies: [Body]);
    NonDeterIteration : (body: Body);
    Body ::= Assignment, BodiesSeq, Test, NonDeterChoice, NonDeterIteration;
    body2s(b: Body, depth: int) -> string;


    Program : (varDeclList: VarDeclList, body: Body);
    program2s(p: Program) -> string;

}


constTree2s(t: Tree<int, Const>) -> string {
    switch (popmax(t)) {
        EmptyPopResult(): "[]";        
        PopResult(key, val, rest): {
            maxIndex = key;
            vals = foldi(
                range(maxIndex + 1),
                "",
                \i, acc, c -> {
                    curConst = lookupTree(t, c);
                    s = eitherMap(curConst, const2s, "_");
                    tmp = acc + s;
                    if (i == maxIndex) tmp else tmp + ", ";
                }
            );
            "[" + vals + "]";
        }
    };
}


exprType2s(t: ExprType) -> string {
    switch (t) {
        Int(): "int";
        Bool(): "bool";
        Array(baseType): "[" + exprType2s(baseType) + "]";
        NoType(): "without_type";
        AnyType(): "can_some_type";
    }
}


varDecl2s(vd: VarDecl) -> string {
    "var " + vd.name + " : " + exprType2s(vd.type) + ";";
}


varDeclList2s(vdl: VarDeclList) -> string {
    fold(vdl.decls, "", \s, d -> {
        s + varDecl2s(d) + "\n";
    })
}


boolConst2s(b: BoolConst) -> string {
    b2s(b.value);
}


const2s(c: Const) -> string {
    switch (c) {
        IntConst(value): i2s(value);
        BoolConst(value): boolConst2s(c);
        ArrayConst(values): constTree2s(values);
    }
}


arithmExpr2s(aexpr: ArithmExpr) -> string {
    withoutParentheses = switch (aexpr) {
        ArithmBinary(l, r, op): {
            ls = expr2s(l);
            rs = expr2s(r);
            switch (op) {
                ArithmAddOp(): ls + " + " + rs;
                ArithmSubOp(): ls + " - " + rs;
                ArithmMulOp(): ls + " * " + rs;
                ArithmDivOp(): ls + " / " + rs;
            }
        }
        ArithmNeg(e): "-" + expr2s(e);
    }

    "(" + withoutParentheses + ")";
}


arrayOp2s(arrayOp: ArrayOp) -> string {
    fst = expr2s(arrayOp.arr);

    switch (arrayOp.opType) {
        ArrayUpdateOp(ind, val): "UPD(" + fst + ", " + expr2s(ind) + ", " + expr2s(val) + ")";

        ArrayApplyOp(ind): "APP(" + fst + ", " + expr2s(ind) + ")";

        ArrayConcatOp(arr2): "CONCAT(" + fst + ", " + expr2s(arr2) + ")";

        ArrayLengthOp(): "LENGTH(" + fst + ")";
    }
}


comparisonType2s(ct: ComparisonType) -> string {
    switch (ct) {
        Equal(): " == ";
        Less(): " < ";
    }
}


boolExpr2s(bexpr: BoolExpr) -> string {
    switch (bexpr) {
        Comparison(l, r, ct): expr2s(l) + comparisonType2s(ct) + expr2s(r);
        Not(e): "not(" + boolExpr2s(e) + ")";
        Or(l, r): "(" + boolExpr2s(l) + ") or (" + boolExpr2s(r) + ")";
        And(l, r): "(" + boolExpr2s(l) + ") and (" + boolExpr2s(r) + ")";
        BoolConst(value): boolConst2s(bexpr);
    }
}


expr2s(e: Expr) -> string {
    switch (e) {
        Var(name): name;
        NonBoolConst(): const2s(e);
        ArithmExpr(): arithmExpr2s(e);
        ArrayOp(arr, opType): arrayOp2s(e);
        BoolExpr(): boolExpr2s(e);
    }
}


bodiesSeq2s(bodies: [Body], depth: int) -> string {
    offset = fold(range(depth), "", \acc, b -> acc + "  ");
    str = foldi(
        bodies, 
        "body{",
        \i, acc, b -> {
            acc + "\n" + offset + body2s(b, depth + 1);
        }
    );
    str + "\n" + offset +"}";
}


nonDeterChoice2s(bodies: [Body], depth: int) -> string {
    offset = fold(range(depth), "", \acc, b -> acc + "  ");
    str = foldi(
        bodies, 
        "choice(",
        \i, acc, b -> {
            acc + "\n" + offset + body2s(b, depth + 1);
        }
    );
    str + "\n" + offset +")";
}


body2s(body: Body, depth: int) -> string {
    switch (body) {
        Assignment(varName, expr): "assigment(" + varName + ", " + expr2s(expr) + ")";

        BodiesSeq(bodies): bodiesSeq2s(bodies, depth);

        Test(bexpr): "test(" + boolExpr2s(bexpr) + ")";

        NonDeterChoice(bodies): nonDeterChoice2s(bodies, depth);

        NonDeterIteration(b): "iteration(" + body2s(b, depth) + ")";
    }
}


program2s(p: Program) -> string {
    varDeclList2s(p.varDeclList) + body2s(p.body, 0);
}

doRange(len : int, acc : [int], index : int) -> [int] {
    if (index == len) {
        acc;
    } else {
        doRange(len, arrayPush(acc, index), index + 1);
    }
}


range(len: int) -> [int] {
    doRange(len, [], 0);
}


findAllAcc(a: [?], fn: (?) -> bool, acc: [?]) -> [?] {
    index = findi(a, fn);
    eitherMap(
        index,
        \i -> concat3([a[i]], acc, findAllAcc(tailFrom(a, i + 1), fn, acc)),
        acc
    );
}


findAll(a: [?], fn: (?) -> bool) -> [?] {    
    findAllAcc(a, fn, []);
}